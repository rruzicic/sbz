import com.sbz.bookstore.model.Order;
import com.sbz.bookstore.model.Item;
import com.sbz.bookstore.model.Book;
import com.sbz.bookstore.model.Genre;

global com.sbz.bookstore.service.ItemService itemService;
global com.sbz.bookstore.service.OrderService orderService;

rule "Two or more item quantity 10% discount"
    activation-group "item discounts" // so that only one rule out of the group is fired
    salience 0 // so that in the rule stack the biggest discount is first
    when
        $i: Item(quantity > 2)
    then
        modify($i){setDiscount($i.getDiscount() + 0.1);}
end

rule "3000 or more item price 5% discount"
    activation-group "item discounts"
    salience -2
    when
        $i: Item(book.getPrice() * quantity >= 3000)
    then
        modify($i){setDiscount($i.getDiscount() + 0.05);}
end

rule "2000 or more item price and education 7% discount"
    activation-group "item discounts"
    salience -1
    when
        $i: Item(book.getPrice() * quantity >= 2000, book.getGenre() == Genre.EDUCATION)
    then
        modify($i){setDiscount($i.getDiscount() + 0.07);}
end


rule "3 or more items in order 10% discount"
    activation-group "order discounts"
    salience -1
    when
        $o: Order(items.size() >= 3)
    then
        modify($o){setDiscount($o.getDiscount() + 0.1);}
        
end

rule "5 or more items in order 15% discount"
    activation-group "order discounts"
    salience 0
    when
        $o: Order(items.size() >= 5)
    then
        modify($o){setDiscount($o.getDiscount() + 0.15);}

end

rule "Calculate order price - items lower price"
    agenda-group "calculate price"
    salience 0
    when
        $o: Order(items.size() > 0, itemService.CalculateOrderItemsPriceWithDiscount(items) < itemService.CalculateOrderItemsPriceWithoutDiscount(items) * (1.0 - discount))
    then
        modify($o){setTotal_price(itemService.CalculateOrderItemsPriceWithDiscount($o.getItems()));}
end

rule "Calculate order price - order lower price"
    agenda-group "calculate price"
    salience 0
    when
        $o: Order(items.size() > 0, itemService.CalculateOrderItemsPriceWithDiscount(items) > itemService.CalculateOrderItemsPriceWithoutDiscount(items) * (1.0 - discount))
    then
        modify($o){setTotal_price(itemService.CalculateOrderItemsPriceWithoutDiscount($o.getItems()) * (1.0 - $o.getDiscount()));}

end